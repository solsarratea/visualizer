<!doctype html>
<html lang="en">

<head>
  <meta charset="utf-8">
  <meta http-equiv="x-ua-compatible" content="ie=edge">
  <title>02 - mandelbulb </title><meta charset="utf-8">
  <title>mandelbulb Visualizer</title>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/110/three.min.js"></script>
  <script src="https://threejs.org/examples/js/controls/OrbitControls.js"></script>
  <script src="https://threejs.org/examples/js/controls/DragControls.js"></script>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/dat-gui/0.7.7/dat.gui.min.js"></script>
  <script src="/core/js/threex.domevents.js"></script>
  <meta name="description" content="visualizing shaders with three.js experiment">
  <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
  <link rel="stylesheet" href="https://use.fontawesome.com/releases/v5.8.1/css/all.css" integrity="sha384-50oBUHEmvpQ+1lW4y57PTFmhCaXp0ML5d60M1M7uH2+nqUivzIebhndOJK28anvf" crossorigin="anonymous">
  <link href="/core/public/styles.css" rel="stylesheet">
</head>
  <body>
    <div class="top-bar">
      <a id="btn" class="info">
        <span class="fab fas fa-info-circle info"></span>
      </a>
      <div id="showInfo" class="modal">
        <div class="modal-header">
          <p>MANDELBULB</p>
          <span class="close">&times;
          </span>
        </div>
        <div class="modal-content">
          <h4>
            This is an implementation of <a href="https://en.wikipedia.org/wiki/Mandelbulb"> the Mandelbulb</a> on Three.js and WebGL that using same distance estimator function as in the Mandelbrot Set.
          </h4>
          <h3> Interactive Controls :  </h3>
          <p> Combine them to explore the boundary
            <p>- ZOOM : scroll in and out. </p>
            <p>- DRAG:  click and move around.</p>
            <p>- DOUBLE CLICK: start/stop continous rotation</p>
          </p>
          <h3> Extra precision controls : on the right of the screen </h3>
          <p> The following controls will be generating a new texture to be rendered on the canvas.</p>
          <p>- bailout: number of precision </p>
          <p>- power: value of the iterative function. Changes shape. </p>
          <p>- rotate: discrete change of angle</p>
            <div class="folder">- COLORIZE FOLDER: 
              <p>Default color rendering is Rainbow
                  <p>- colors: create pallete </p>
                  <p>- Saturation </p>
                  <p>- Brightness </p>
              </p>
            </div>
          <p class="footer">
            <p><a href="https://www.recurse.com/" class="RC">
              <img src="https://avatars0.githubusercontent.com/u/1085202?s=200&v=4"/>  Developed at Recurse Center </a></p>
              This is a still WIP for any comments drop me at <a href="contact@solquemal.com">contact@solquemal.com</a></p>
            </p>
        </div>
      </div>
    <a href="https://github.com/solsarratea/visualizer/tree/master/02-mandelbulb" class="info" title="View source on Github" >
      <span class="fab fa-github"></span>
      <span class="text">View source</span>
    </a>
  <script id="vertexShader" type="x-shader/x-fragment">
          varying vec2 vUv;

          void main()
          {
            vUv = uv;
            vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
            gl_Position = projectionMatrix * mvPosition;
          }
   </script>
  <script id="mandelbulbFragmentShader" type="x-shader/x-fragment">
          uniform float time;
          uniform float camX;
          uniform float camY;
          uniform float camZ;
          uniform float zoom;
          uniform vec3 colorA;
          uniform vec3 colorB;
          uniform float t;
          varying vec2 vUv;
          uniform float power;
          uniform float rotate;
          uniform float light;
          uniform float bailout;
          
          #define PHI (sqrt(5)*0.5 + 0.5)

          //Color functions
          vec3 palette( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d )
          {
              return a + b*cos( 9.28*(c*t+d) );
          }

          void pR(inout vec2 p, float a) {
              p = cos(a)*p + sin(a)*vec2(p.y, -p.x);
          }


          //FRACTAl 3D
          float DE3(vec3 pos) {
              vec3 z = pos;
              float dr = 1.0;
              float r = 0.0;
              for (int i = 0; i < 10 ; i++) {
                  r = length(z);
                  if (r>bailout) break;
                  
                  // convert to polar coordinates
                  float theta = acos(z.z/r);
                  float phi = atan(z.y,z.x);
                  dr =  pow( r, power-1.0)*power*dr ;

                  // scale and rotate the point
                  float zr = pow( r,power);
                  theta = theta*power;
                  phi = phi*power;
                  
                  // convert back to cartesian coordinates
                  z = zr*vec3(sin(theta)*cos(phi), sin(phi)*sin(theta), cos(theta));
                  z+=pos;
              }
              return 0.3*log(r)*r/dr;
          }



          float scene(vec3 ray){
          
              ray = ray-vec3(0.03,0.5,1.);
              vec3 ray2 = ray;
              //Spin it - change the plane
              pR(ray2.zx,rotate); // rotating
              
               return max(DE3(ray2),0.);
          }

          vec3 estimateNormal(vec3 p) {
              float smallNumber = 0.1;
              vec3 n = vec3(
              scene(vec3(p.x + smallNumber, p.yz)) -
              scene(vec3(p.x - smallNumber, p.yz)),
              scene(vec3(p.x, p.y + smallNumber, p.z)) -
              scene(vec3(p.x, p.y - smallNumber, p.z)),
              scene(vec3(p.xy, p.z + smallNumber)) -
              scene(vec3(p.xy, p.z - smallNumber))
          );

          return normalize(n);
          }


          float lighting(vec3 origin, vec3 dir, vec3 normal) {
              vec3 lightPos = vec3(cos(time/20.)*5., sin(time), 1.);
              vec3 lightv = normalize(lightPos - origin);
              
              float diffuse = max(0., dot(lightv, normal));
              vec3 reflectedRay = light* dot(lightv, normal) * normal - lightv;
              
              float specular = max(0.3, (pow(dot(reflectedRay, lightv), 4.)));
              float ambient = 0.08;
              
              return ambient + diffuse + specular;

          }

          vec4 trace(vec3 rayOrigin, vec3 dir){
              vec3 ray = rayOrigin;
              float dist = 0.; 
              float totalDist = 0.;
              float maxDist = 5.;
              
              for (int i = 0; i < 40 ; i++){
                  dist = scene(ray);
              
                  if(dist < 0.0001){
                      vec4 distCol = vec4(1. - vec4(totalDist/maxDist));
                      vec3 hsvCol = palette(
                          distCol.b,
                          colorA/255.,
                          colorB/255.,
                          vec3(.5, 1.0, .8),
                          vec3(.6, .4, .2));
                      vec4 lightingCol = vec4(lighting(rayOrigin,dir,estimateNormal(ray)));
                      return vec4(hsvCol,1.)*t + lightingCol*(1. - t);
                  } 
                  totalDist += dist;
                  ray += dist * dir;
              }   
              return vec4(0.);
          }

          vec3 lookAt(vec2 uv, vec3 camOrigin, vec3 camTarget){
              vec3 zAxis = normalize(camTarget - camOrigin);
              vec3 up = vec3(-0.03,0.4,0.2);
              vec3 xAxis = normalize(cross(up, zAxis));
              vec3 yAxis = normalize(cross(zAxis, xAxis));

              float fov = 2.;

              vec3 dir = (normalize(uv.x * xAxis + uv.y * yAxis + zAxis * fov));

              return dir;
          }


          void main() {
              vec2 v = vUv;
              v = v *4. - 2.;
              v *= pow( 0.7, 5.0 + 13.*(1.+zoom));
          
              vec3 camOrigin = vec3(camX, camY, camZ); //z axis foward and backwards
              vec3 camTarget = vec3(0, 0, 0);
              vec3 rayOrigin = vec3(camOrigin.xy, camOrigin.z - 2.5);  
          
              vec3 direction = lookAt(v, camOrigin, camTarget);
              gl_FragColor = trace(rayOrigin, direction);
          
          }
  </script>
  <script src="/core/js/index-controls.js"></script>
  <script src="../dist/mandelbulb.bundle.js"></script> <!-- Use in production -->
  <!-- <script src="http://localhost:9001/dist/mandelbulb.bundle.js"></script> --> <!-- Use in development -->
</body>

</html>